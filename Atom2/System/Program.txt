load-file System.txt pragma

(mscorlib) import
("System.Windows.Forms, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089") import-with-full-name

(System.Windows.Forms) use

(
  (menu-item menu-text click-handler) put
  menu-item (DropDownItems) call (menu-text Add) call ((click-handler) get create-event-handler Click) call   
)
(add-menu-item) set

(
  (menu-item-message) put
  (drop-2) verbatim
  (menu-item-message) get
  (message) verbatim
  3 join
)
(define-simple-event-handler) set 

(
  TextBox (new) call (textBox) set
  textBox (true Multiline) call 
  textBox (DockStyle (Fill) call Dock) call

  Button (new) call (button) set
  button (DockStyle (Top) call Dock) call
  button ("Press" Text) call 
  button ((drop drop textBox (("Hello World!") verbatim Text) call ) create-event-handler Click) call

  MenuStrip (new) call (mainMenuStrip) set
  mainMenuStrip (Items) call ("File" Add) call (fileMenuItem) set
  (fileMenuItem "New" "New" define-simple-event-handler add-menu-item) call
  (fileMenuItem "Open" (drop-2 "Open" message) add-menu-item) call
  (fileMenuItem "Save" (drop-2 "Save" message) add-menu-item) call
  mainMenuStrip (Items) call (("Edit") verbatim Add) call (editMenuItem) set
  (editMenuItem "Cut" (drop-2 "Cut" message) add-menu-item) call
  (editMenuItem "Copy" (drop-2 "Copy" message) add-menu-item) call
  (editMenuItem "Paste" (drop-2 "Paste" message) add-menu-item) call

  Form (new) call (form) set
  form (Controls) call (textBox Add) call
  form (Controls) call (button Add) call
  form (Controls) call (mainMenuStrip Add) call
  (form) get
)
(new-main-form) set

Application (new-main-form Run) call
